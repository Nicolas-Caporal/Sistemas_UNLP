{ 1- Clientes y Servidores
Existe un robot que sirve de flores a tres robots clientes. Cada cliente solicita al servidor que
le deposite en su esquina siguiente una cantidad de flores aleatoria (entre 1 y 4). Por
ejemplo, si el cliente se encuentra en la esquina (2,1) le solicitar√° que coloque x cantidad de
flores en la esquina (2,2).
Cuando el robot servidor deposita las flores en la esquina solicitada, el cliente las junta y las
deposita una a una a lo largo de la avenida en la que se encuentra.
El programa finaliza cuando todos los robos clientes completan su avenida. Asuma que el
robot servidor tiene flores suficientes en su bolsa.
El robot servidor se inicia en la esquina (100,100)
Los robots clientes inician en las esquinas (1,1) , (2,1) y (3,1) respectivamente }

programa Ejercicio1Practica4

procesos
  proceso juntarFlores 
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
  fin
  
  proceso avanzarDejandoFlores
  comenzar
    mientras ((HayFlorEnLaBolsa) & (PosCa < 100))
      depositarFlor
      mover
  fin
  
  proceso asignarIds
  comenzar
    EnviarMensaje(1, C1)
    EnviarMensaje(2, C2)
    EnviarMensaje(3, C3)
  fin
  
  proceso dejarFlores (E N: numero)
  comenzar
    repetir N
      depositarFlor
  fin

areas
  areaC1: AreaP(1,1,1,1)
  areaC2: AreaP(2,1,2,1)
  areaC3: AreaP(3,1,3,1)
  areaServidor: AreaP(100,100,100,100)
  avenida1: AreaPC(1,2,1,100)
  avenida2: AreaPC(2,2,2,100)
  avenida3: AreaPC(3,2,3,100)
  
robots
  robot cliente
  variables
    id: numero
    cantFlores: numero
    avAct: numero
    caSig: numero
    listo: boolean
  comenzar
    RecibirMensaje(id, server)
    mientras (PosCa < 99)
      Random(cantFlores, 1, 4)
      EnviarMensaje(id, server)
      EnviarMensaje(cantFlores, server)
      avAct := PosAv
      EnviarMensaje(avAct, server)
      caSig := PosCa + 1
      EnviarMensaje(caSig, server)
      listo := F
      RecibirMensaje(listo, server)
      Pos(avAct, caSig)
      juntarFlores
      Pos(avAct, caSig - 1)
      avanzarDejandoFlores
    EnviarMensaje(id, server)
    EnviarMensaje(0, server)   
  fin
  
  robot servidor
  variables
    terminaron: numero
    id: numero
    av, ca: numero
    cantF: numero
  comenzar
    asignarIds
    terminaron := 0
    mientras (terminaron < 3)
      RecibirMensaje(id, *)
      si (id = 1)
        RecibirMensaje(cantF, C1)
        si ~(cantF = 0)
          RecibirMensaje(av, C1)
          RecibirMensaje(ca, C1)
          Pos(av, ca)
          dejarFlores(cantF)
          Pos(100, 100)
          EnviarMensaje(V, C1)
        sino
          terminaron := terminaron + 1
          
      sino
        si (id = 2)
          RecibirMensaje(cantF, C2)
          si ~(cantF = 0)
            RecibirMensaje(av, C2)
            RecibirMensaje(ca, C2)
            Pos(av, ca)
            dejarFlores(cantF)
            Pos(100, 100)
            EnviarMensaje(V, C2)
          sino
            terminaron := terminaron + 1

        sino
          RecibirMensaje(cantF, C3)
          si ~(cantF = 0)
            RecibirMensaje(av, C3)
            RecibirMensaje(ca, C3)
            Pos(av, ca)
            dejarFlores(cantF)
            Pos(100, 100)
            EnviarMensaje(V, C3)
          sino
            terminaron := terminaron + 1
  fin
  
variables
  C1: cliente
  C2: cliente
  C3: cliente
  server: servidor
  
comenzar
  AsignarArea(C1, areaC1)
  AsignarArea(C1, avenida1)
  AsignarArea(C2, areaC2)
  AsignarArea(C2, avenida2)
  AsignarArea(C3, areaC3)
  AsignarArea(C3, avenida3)
  AsignarArea(server, areaServidor)
  AsignarArea(server, avenida1)
  AsignarArea(server, avenida2)
  AsignarArea(server, avenida3)
  
  Iniciar(C1, 1,1)
  Iniciar(C2, 2,1)
  Iniciar(C3, 3,1)
  Iniciar(server, 100,100)
fin