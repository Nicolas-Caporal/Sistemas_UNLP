{3- Realice un programa donde 4 robots colaboren para recoger todas las flores de una
esquina indicada por un robot jefe, seleccionada de manera aleatoria dentro del cuadrante
(2,2) y (10,10). Para ello el jefe determina inicialmente una esquina y los robots deben
accederla, tomar de a una las flores y volver a su posición inicial. Cuando los robots
terminan el jefe deberá informar cuál de ellos logró recoger más flores.
Las esquinas de inicio de los robots deberán ser jefe (1,1) y robots (2,1), (3, 1), (4,1) y (5,1).}

programa Ejercicio3Practica3

procesos
  proceso enviarIds
  comenzar
    EnviarMensaje(1, robot1)
    EnviarMensaje(2, robot2)
    EnviarMensaje(3, robot3)
    EnviarMensaje(4, robot4)
  fin
  
  proceso enviarEsquina (E av: numero; E ca: numero)
  comenzar
    EnviarMensaje(av, robot1)
    EnviarMensaje(av, robot2)
    EnviarMensaje(av, robot3)
    EnviarMensaje(av, robot4)
    EnviarMensaje(ca, robot1)
    EnviarMensaje(ca, robot2)
    EnviarMensaje(ca, robot3)
    EnviarMensaje(ca, robot4)
  fin

  proceso autorizarAEnviarCant 
  comenzar
    EnviarMensaje(1, robot1)
    EnviarMensaje(1, robot2)
    EnviarMensaje(1, robot3)
    EnviarMensaje(1, robot4)
  fin

areas
  inicioJefe: AreaP(1,1,1,1)
  inicio1: AreaP(2,1,2,1)
  inicio2: AreaP(3,1,3,1)
  inicio3: AreaP(4,1,4,1)
  inicio4: AreaP(5,1,5,1)
  cuadrante: AreaPC(2,2, 10,10)

robots
  robot trabajador
  variables
    iniAv, iniCa, av, ca: numero
    id: numero
    HayFlores: boolean
    cant: numero
    autorizacion: numero
    
  comenzar
    cant := 0
    HayFlores := V
    iniAv := PosAv
    iniCa := PosCa
    RecibirMensaje(id, jefe)
    RecibirMensaje(av, jefe)
    RecibirMensaje(ca, jefe)
    mientras HayFlores
      BloquearEsquina(av, ca)
      EnviarMensaje(id, jefe)
      RecibirMensaje(HayFlores, jefe)
      si (HayFlores)
        Pos(av, ca)
        si (HayFlorEnLaEsquina)
          tomarFlor
          cant := cant + 1
        sino
          HayFlores := F
          
      Pos(iniAv, iniCa)
      LiberarEsquina(av, ca)
      EnviarMensaje(HayFlores, jefe) 
      
    RecibirMensaje(autorizacion, jefe)
    EnviarMensaje(id, jefe)
    EnviarMensaje(cant, jefe)
  fin
  
  robot fiscal
  variables
    av, ca: numero
    HayFlores: boolean
    id: numero
    cant, cantMax, idMax: numero
  comenzar
    enviarIds
    Random(av, 2, 10)
    Random(ca, 2, 10)
    enviarEsquina(av, ca)
    HayFlores := V
    
    mientras HayFlores
      RecibirMensaje(id, *)
      si (id = 1)
        EnviarMensaje(HayFlores, robot1)
        RecibirMensaje(HayFlores, robot1)
      sino
        si (id = 2)
          EnviarMensaje(HayFlores, robot2)
          RecibirMensaje(HayFlores, robot2)
        sino
          si (id = 3)
            EnviarMensaje(HayFlores, robot3)
            RecibirMensaje(HayFlores, robot3)
          sino
            EnviarMensaje(HayFlores, robot4)
            RecibirMensaje(HayFlores, robot4)
        
    repetir 3
      RecibirMensaje(id, *)
      si (id = 1)
        EnviarMensaje(HayFlores, robot1)
        RecibirMensaje(HayFlores, robot1)
      sino
        si (id = 2)
          EnviarMensaje(HayFlores, robot2)
          RecibirMensaje(HayFlores, robot2)
        sino
          si (id = 3)
            EnviarMensaje(HayFlores, robot3)
            RecibirMensaje(HayFlores, robot3)
          sino
            EnviarMensaje(HayFlores, robot4)
            RecibirMensaje(HayFlores, robot4)
    
    cantMax := -1
    idMax := -1
    autorizarAEnviarCant
    repetir 4
      RecibirMensaje(id, *)
      si (id = 1)
        RecibirMensaje(cant, robot1)
      sino
        si (id = 2)
          RecibirMensaje(cant, robot2)
        sino
          si (id = 3)
            RecibirMensaje(cant, robot3)
          sino
            RecibirMensaje(cant, robot4)
      
      si (cant > cantMax)
        cantMax := cant
        idMax := id
    
    Informar('Ganador', idMax)
  fin
  
variables
  robot1, robot2, robot3, robot4: trabajador
  jefe: fiscal
  
comenzar
  AsignarArea(jefe, inicioJefe)
  AsignarArea(robot1, inicio1)
  AsignarArea(robot2, inicio2)
  AsignarArea(robot3, inicio3)
  AsignarArea(robot4, inicio4)
  
  AsignarArea(robot1, cuadrante)
  AsignarArea(robot2, cuadrante)
  AsignarArea(robot3, cuadrante)
  AsignarArea(robot4, cuadrante)
  
  Iniciar(jefe, 1,1)
  Iniciar(robot1, 2,1)
  Iniciar(robot2, 3,1)
  Iniciar(robot3, 4,1)
  Iniciar(robot4, 5,1)
fin
