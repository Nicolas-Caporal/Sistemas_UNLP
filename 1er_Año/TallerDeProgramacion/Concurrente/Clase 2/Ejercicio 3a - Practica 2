{3. Realice un programa con 2 equipos:
- El equipo A, compuesto por los robots A1 y A2, debe juntar papeles de las
primeras 20 esquinas de la calle 1
- El equipo B, compuesto por los robots B1 y B2, debe juntar flores de las primeras
20 esquinas de la calle 5
Los robots A1 y B1 deberán realizar las 10 primeras esquinas de su recorrido, y al
finalizar avisarán a los robots A2 y B2 respectivamente para que continúen con las
siguientes 10 esquinas. El segundo robot de cada equipo debe informar la cantidad
de elementos recogidos en las 20 esquinas.
Inicialice los 4 robots en las esquinas que considere más apropiadas según el
trayecto que le corresponde realizar a cada uno. }
programa Ejercicio3Practica2
procesos
  proceso juntarPapeles (ES cantP: numero)
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
      cantP := cantP + 1
  fin
  
  proceso juntarFlores (ES cantF: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      cantF := cantF + 1
  fin
  
  proceso recorrerCalleA (ES cantP: numero)
  comenzar
    repetir 9
      juntarPapeles(cantP)
      mover
    juntarPapeles(cantP)
  fin
  
  proceso recorrerCalleB (ES cantF: numero)
  comenzar
    repetir 9
      juntarFlores(cantF)
      mover
    juntarFlores(cantF)
  fin

areas
  areaA1: AreaP(1,1,10,1)
  areaA2: AreaP(11,1,20,1)
  areaB1: AreaP(1,5,10,5)
  areaB2: AreaP(11,5,20,5)
  
robots
  robot equipoA
  variables
    cant: numero
  comenzar
    derecha
    si (PosAv = 1)
      cant := 0
    sino
      RecibirMensaje(cant, A1)
    recorrerCalleA(cant)
    si (PosAv = 10)
      EnviarMensaje(cant, A2)
    sino
      Informar(cant)
  fin
  
  robot equipoB
  variables
    cant: numero
  comenzar
    derecha
    si (PosAv = 1)
      cant := 0
    sino
      RecibirMensaje(cant, B1)
    recorrerCalleB(cant)
    si (PosAv = 10)
      EnviarMensaje(cant, B2)
    sino
      Informar(cant)
  fin
  
variables
  A1: equipoA
  A2: equipoA
  B1: equipoB
  B2: equipoB
  
comenzar
  AsignarArea(A1, areaA1)
  AsignarArea(A2, areaA2)
  AsignarArea(B1, areaB1)
  AsignarArea(B2, areaB2)
  
  Iniciar(A1, 1,1)
  Iniciar(A2, 11,1)
  Iniciar(B1, 1, 5)
  Iniciar(B2, 11, 5)
fin